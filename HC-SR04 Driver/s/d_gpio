.extern Div_10

.equ DIR_FISICA, 0X20200000 
.equ GPSET, 0X1C
.equ GPCLR, 0X28
.equ GPLEV, 0X34
.equ NUM_COMPROB, 100000 @comprobaciones

.data
.align 2
DIR_BASE: .space 4
LEDS: .byte 9, 10, 11, 17, 22, 27, 4, 14
@numeros de terminal: LEDS, Zumbador, Trig HC-SR04

.globl prog_gpio
.globl valor_terminal
.globl espera_valor

.text
prog_gpio:
STMDB SP!, {R4-R8, LR}
MOV R4, R0 @NUMERO PIN
MOV R5, R1 @NUMERO ESTADO (0 IN, 1 OUT)
LDR R1, =DIR_FISICA
MOV R0, #13
MOV R2, #0X100
SWI 0X68
LDR R0, =DIR_BASE
STR R3, [R0] @STORE DIR_BASE
MOV R6, R3 @DIRECCION GPFSEL0
MOV R0, R4
BL Div_10
LSL R8, R0, #2
ADD R6, R6, R8 @DIRECCION GPFSELX
MOV R7, #3
MUL R7, R1, R7 @No PRIMER BIT ESTADO DEL PIN
MOV R0, #7
LSL R0, R0, R7
MOV R1, R5
LSL R1, R1, R7
SWI 0X16
LDR R8, [R6]
BIC R8, R8, R0
ORR R8, R8, R1
STR R8, [R6]
SWI 0X7C
LDMIA SP!, {R4-R8, PC}

valor_terminal:
STMDB SP!, {R4-R8, LR}
MOV R4, R0 @No LED
MOV R5, R1 @ACTIVADO/DESACTIVADO
LDR R7, =LEDS
SUB R4, R4, #1
LDRB R7, [R7, R4]
MOV R8, #1
LSL R8, R8, R7
LDR R6, =DIR_BASE
LDR R6, [R6]  @LOAD DIR_BASE
CMP R5, #0
SWI 0X16
STRNE R8, [R6, #GPSET]
STREQ R8, [R6, #GPCLR]
SWI 0X7C
LDMIA SP!, {R4-R8, PC}

espera_valor:
@recibe como parametros el valor del terminal y el valor esperado en este. Retorna 0 si no se ha detectado ese valor, y 1 si lo ha detectado.
STMDB SP!, {R4-R8, LR}
LDR R4, =NUM_COMPROB

BUCLE_EV:
CMP R4, #0
BLE FIN_NO_DETECTADO

MOV R5, #1
LSL R5, R5, R0 @mascara
LDR R6, =DIR_BASE
LDR R6, [R6]  @LOAD DIR_BASE
SWI 0X16
LDR R7, [R6, #GPLEV]
SWI 0X7C  
LSR R8, R7, R0
AND R8, R8, #1
CMP R8, R1
MOVEQ R0, #1 @true
BEQ FIN_DETECTADO

SUB R4, R4, #1
B BUCLE_EV

FIN_NO_DETECTADO:
MOV R0, #0 @false

FIN_DETECTADO:
LDMIA SP!, {R4-R8, PC}


